setopt autocd
setopt correct
setopt histignorealldups
setopt sharehistory
HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000

# Set UTF-8 locale to properly handle Unicode characters (especially powerline glyphs)
# This fixes cursor positioning issues with ambiguous-width characters in SSH sessions
if [[ -z "$LANG" ]] || [[ "$LANG" == "POSIX" ]] || [[ "$LANG" == "C" ]]; then
  if locale -a 2>/dev/null | grep -qi "en_US.utf8\|en_US.UTF-8"; then
    export LANG="en_US.UTF-8"
    export LC_ALL="en_US.UTF-8"
  elif locale -a 2>/dev/null | grep -qi "C.utf8\|C.UTF-8"; then
    export LANG="C.UTF-8"
    export LC_ALL="C.UTF-8"
  fi
fi

if command -v nvim >/dev/null 2>&1; then
  export EDITOR="nvim"
elif command -v vim >/dev/null 2>&1; then
  export EDITOR="vim"
else
  export EDITOR="vi"
fi

if command -v eza >/dev/null 2>&1; then
  alias ls='eza -alh --group-directories-first --icons=auto'
else
  alias ls='ls --color=auto -alh'
fi

if command -v bat >/dev/null 2>&1; then
  alias bat='bat'
elif command -v batcat >/dev/null 2>&1; then
  alias bat='batcat'
fi

# Copy bat output to the clipboard without decorations
batclip() {
  local -a bat_cmd copier

  if command -v bat >/dev/null 2>&1; then
    bat_cmd=(bat)
  elif command -v batcat >/dev/null 2>&1; then
    bat_cmd=(batcat)
  else
    print -u2 "batclip: bat is not installed"
    return 1
  fi

  if command -v clip.exe >/dev/null 2>&1; then
    copier=(clip.exe)
  elif command -v wl-copy >/dev/null 2>&1; then
    copier=(wl-copy)
  elif command -v xclip >/dev/null 2>&1; then
    copier=(xclip -selection clipboard)
  elif command -v xsel >/dev/null 2>&1; then
    copier=(xsel --clipboard --input)
  else
    print -u2 "batclip: no clipboard command available"
    return 1
  fi

  "${bat_cmd[@]}" --style=plain --color=never --paging=never "$@" | "${copier[@]}"
}

if command -v fd >/dev/null 2>&1; then
  alias fd='fd'
elif command -v fdfind >/dev/null 2>&1; then
  alias fd='fdfind'
fi

if [[ -r /etc/os-release ]]; then
  . /etc/os-release
  if [[ -d /etc/pve ]] || echo "${PRETTY_NAME:-}" | grep -qi proxmox; then
    [[ -f "$HOME/.dotfiles/shell/zshrc.d/proxmox.zsh" ]] && source "$HOME/.dotfiles/shell/zshrc.d/proxmox.zsh"
  else
    case "$ID" in
      arch)
        [[ -f "$HOME/.dotfiles/shell/zshrc.d/arch.zsh" ]] && source "$HOME/.dotfiles/shell/zshrc.d/arch.zsh"
        ;;
      debian|ubuntu)
        [[ -f "$HOME/.dotfiles/shell/zshrc.d/debian.zsh" ]] && source "$HOME/.dotfiles/shell/zshrc.d/debian.zsh"
        ;;
    esac
  fi
fi

# Old prompt commented out in favor of Starship
# PROMPT='%F{cyan}%n%f@%F{yellow}%m%f:%F{green}%~%f %# '

# Fix terminal compatibility issues
case "$TERM" in
  xterm-ghostty*|*ghostty*)
    # Fallback to compatible terminal type if ghostty terminfo is not available
    if ! infocmp "$TERM" >/dev/null 2>&1; then
      export TERM="xterm-256color"
    fi
    ;;
esac

# Initialize Starship prompt
if command -v starship >/dev/null 2>&1; then
  eval "$(starship init zsh)"
fi

if command -v zoxide >/dev/null 2>&1; then
  eval "$(zoxide init zsh)"
fi

if command -v atuin >/dev/null 2>&1; then
  eval "$(atuin init zsh)"
fi

if [[ -f /usr/share/fzf/key-bindings.zsh ]]; then
  source /usr/share/fzf/key-bindings.zsh
elif [[ -f /usr/share/doc/fzf/examples/key-bindings.zsh ]]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh
fi

if [[ -z "${FZF_DEFAULT_OPTS:-}" ]]; then
  export FZF_DEFAULT_OPTS="--height 40% --layout=reverse --border"
fi

alias ya="yazi"
